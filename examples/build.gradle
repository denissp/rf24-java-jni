buildscript {
	repositories {
		mavenCentral()
	}
	dependencies {
		classpath 'com.github.rholder:gradle-one-jar:1.0.4'
	}
}

group rootProject.group
version rootProject.version

apply plugin: 'java'
apply plugin: 'gradle-one-jar'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

ext {
	examples = ['Simple', 'Rf24PingPong']
}

dependencies {
    compile group: 'commons-cli', name: 'commons-cli', version: '1.3'
    compile rootProject
}


// Create Jar task
project.examples.forEach { String taskName ->
	final fullTaskName = "jar${taskName}Example"
	tasks.create(name: fullTaskName, type: OneJar) {
		group 'Build examples JARs'
		mainClass = "pl.grzeslowski.smarthome.rf24.examples.${taskName}Example"
		archiveName = "$project.name-${taskName.toLowerCase()}-example.jar"
	}
	tasks[fullTaskName].dependsOn rootProject.tasks.compileRf24
}


// Create run task
project.examples.forEach { String taskName ->
	final fullTaskName = "run${taskName}Example"
	final jarTask = "jar${taskName}Example"
	tasks.create(name: fullTaskName, type: Exec) {
		group 'Run Examples'
		workingDir "$project.buildDir/libs"
		commandLine "sudo", "java", "-jar", "-Djava.library.path=$rootProject.wrapperBuildDir", tasks[jarTask].archiveName
	}
	tasks[fullTaskName].dependsOn += tasks[jarTask]
}
